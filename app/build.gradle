apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.lyricgan.demo.util"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        flavorDimensions "default"

        // dex limit for 65535 methods
        multiDexEnabled true

        manifestPlaceholders = [APP_CHANNEL_VALUE: "common"]
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"

            minifyEnabled false
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == 'release') {
                variant.outputs.all {
                    // apk output with AndroidUtils_v1.0_2018-01-01_common.apk
                    outputFileName = "AndroidUtils_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                }
            }
        }
    }

    productFlavors {
        common {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [APP_CHANNEL_VALUE: name]
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'
}

def static releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def supportLibraryVersion = '27.1.1'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    implementation project(":library_util")

    implementation 'com.android.support:cardview-v7:' + supportLibraryVersion
    implementation 'com.android.support:design:' + supportLibraryVersion
    implementation 'com.android.support:support-v4:' + supportLibraryVersion
    implementation 'com.android.support:appcompat-v7:' + supportLibraryVersion
    implementation 'com.android.support:recyclerview-v7:' + supportLibraryVersion

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    implementation 'com.google.zxing:core:3.3.1'
    implementation 'com.nineoldandroids:library:2.4.0'
}
